$version 11.00

$rect <20,10,220,50>
inline Inline
{
  #include "TEST_pub.h"
  #include "VI_pub.h"
  #include "ew_priv.h"
}

$rect <20,60,260,100>
$output false
class DEV_Main : Menu::BaseMenuView
{
  $rect <510,10,710,50>
  inherited method LoadItemClass()
  {
    var class ItemClass = null;
    switch (aItemNo )
    {
      case 0, 1: ItemClass = Menu::ItemCheckbox;
      default: ItemClass = Menu::ItemBase;
    }

    return ItemClass;
  }

  $rect <510,60,710,100>
  inherited method LoadItemTitle()
  {
    var string Title = "";
    if( aItemNo < ItemTitleArray.size )
    {
      Title = ItemTitleArray[aItemNo];
    }
    return Title;
  }

  $rect <510,160,710,200>
  inherited method OnItemActivate()
  {
    aMenuItem; //suppress warnin

    switch( aItemNo )
    {
      case 0:
      {
        if( Enum::OperationMode.FACTORY == DeviceInterface::SystemDevice.OperationMode )
        {
          DeviceInterface::SystemDevice.OperationMode = Enum::OperationMode.NORMAL;
        }
        else
        {
          DeviceInterface::SystemDevice.OperationMode = Enum::OperationMode.FACTORY;
        }
      }
      case 1: DeviceInterface::SystemDevice.IsHopperTestMode = !DeviceInterface::SystemDevice.IsHopperTestMode;
      case 2: PresentDialog( new Factory::Main, null, null, null, null, null, null, null, null, false );
      case 3: PresentDialog( new Development::DEV_LanguageTest, null, null, null, null, null, null, null, null, false );
      case 4: PresentDialog( new Development::DEV_RealTimeClock, null, null, null, null, null, null, null, null, false );
      case 5: PresentDialog( new Development::DEV_Bluetooth, null, null, null, null, null, null, null, null, false );
      case 6: PresentDialog( new Development::DEV_TFTBrightness, null, null, null, null, null, null, null, null, false );
      case 7: PresentDialog( new Development::DEV_VehicleSupportedFunction, null, null, null, null, null, null, null, null, false );
      case 8: PresentDialog( new Development::DEV_MotoCon, null, null, null, null, null, null, null, null, false );
      case 9: PresentDialog( new Development::DEV_FontMenu, null, null, null, null, null, null, null, null, false );
      case 10:
      {
        var Inspection::TFT_Main InspectionDialog = new Inspection::TFT_Main;
        InspectionDialog.PassUpKey = false;
        InspectionDialog.PassDownKey = false;
        InspectionDialog.PassHomeKey = false;
        PresentDialog( InspectionDialog, null, null, null, null, null, null, null, null, false );
      }
      case 11: PresentDialog( new Development::DEV_EEPROMTest, null, null, null, null, null, null, null, null, false );
      case 12:
      {
        // Test if YDT dialog is popped again when the YDT dialog is already displayled.
        var int32 i;
        for( i = 0; i < 3; i++ )
        {
          DeviceInterface::VehicleDevice.NotifyDataReceived( Enum::VehicleRxType.YDT_DETECTED );
        }
      }
      case 13: PresentDialog( new Navigation::NAV03_TBTListView, null, null, null, null, null, null, null, null, false );
      case 14:PresentDialog( new Navigation::NAV05_TBTView, null, null, null, null, null, null, null, null, false );
      case 15: PresentDialog( new Weather::WEA01_Main, null, null, null, null, null, null, null, null, false );
      default:;
    }
  }

  $rect <510,110,710,150>
  inherited method LoadItemChecked()
  {
    var bool IsChecked = false;
    switch( aItemNo )
    {
      case 0: IsChecked = ( Enum::OperationMode.NORMAL != DeviceInterface::SystemDevice.OperationMode );
      case 1: IsChecked = DeviceInterface::SystemDevice.IsHopperTestMode;
      default:;
    }
    return IsChecked;
  }

  $rect <20,20,160,60>
  inherited object Menu
  {
    preset NoOfItems = ItemTitleArray.size;
    preset ItemHeight = Menu::ItemHeightSmall;
    preset ItemNumPerPage = 4;
  }

  $rect <730,60,930,100>
  array string ItemTitleArray[ 16 ] =
  (
    Default[0] = "Factory Mode";
    Default[1] = "Hopper Test Mode";
    Default[2] = "Factory Test";
    Default[3] = "Language";
    Default[4] = "Real Time Clock";
    Default[5] = "Bluetooth Test";
    Default[6] = "TFT Brightness Test";
    Default[7] = "Vehicle Supported Func";
    Default[8] = "MotoCon Test";
    Default[9] = "Font Test";
    Default[10] = "Inspection Test";
    Default[11] = "EEPROM Test";
    Default[12] = "YDT Test";
    Default[13] = "TBT List Test";
    Default[14] = "TBT View Test";
    Default[15] = "Weather View Test";
  );

  $reorder HideFocusFrameTimer 1
  $reorder OnHideFocusFrameTimeoutSlot 1
  $reorder OnDialogSlideInCompletedSlot 1
  $reorder PresentDialogWithSlideInEffect 1
  $reorder SlideOutEffectEnabled 1
  $reorder LoadItemClass 4
  $reorder LoadItemTitle 4
  $reorder OnItemActivate 3
  $reorder LoadItemChecked 3
  $reorder DismissMenuWithSlideOutEffect 1
  $reorder ItemTitleArray 2
}

$rect <20,110,260,150>
$output false
class DEV_RealTimeClock : Components::BaseMainBG
{
  $rect <10,290,210,330>
  inherited property Bounds = <0,0,480,272>;

  $rect <220,290,450,330>
  inherited method OnShortHomeKeyActivated()
  {
    Owner.DismissDialog( this, null, null, null, null, null, false );
  }

  $rect <530,120,760,160>
  slot OnUpdateLocalTimeSlot
  {
    var DeviceInterface::RtcTime CurrentTime = DeviceInterface::SystemDevice.GetLocalTime();

    TimeText.String = string( CurrentTime.Year, 4 ) + "/" + string( CurrentTime.Month, 2 ) + "/"+ string( CurrentTime.Day, 2 ) + " "
                      + string( CurrentTime.Hour, 2 ) + ":" + string( CurrentTime.Minute, 2 ) + ":" + string( CurrentTime.Second, 2 );
  }

  $rect <20,20,160,60>
  object Views::Text TimeText
  {
    preset Bounds = <96,79,390,122>;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset String = "";
    preset Font = Fonts::NotoSansCjkJpMedium24pt;
  }

  $rect <530,70,760,110>
  object Core::Timer UpdateLocalTimeTimer
  {
    preset OnTrigger = OnUpdateLocalTimeSlot;
    preset Period = 500;
    preset Enabled = true;
  }
}

$rect <20,160,260,200>
$output false
class DEV_Bluetooth : Components::BaseMainBG
{
  $rect <10,290,210,330>
  inherited property Bounds = <0,0,480,272>;

  $rect <220,290,450,330>
  inherited method OnShortHomeKeyActivated()
  {
    Owner.DismissDialog( this, null, null, null, null, null, false );
  }

  $rect <20,20,160,60>
  object Views::Text BtSwVersionTitle
  {
    preset Bounds = <0,40,240,65>;
    preset String = "BT Software Version";
    preset Font = Fonts::NotoSansCjkJpMedium24pt;
  }

  $rect <20,20,160,60>
  object Views::Text BtSwVersionText
  {
    preset Bounds = <247,40,356,65>;
    preset Alignment = Views::TextAlignment[AlignHorzLeft, AlignVertCenter];
    preset String = DeviceInterface::SystemDevice.BtSoftwareVersion;
    preset Font = Fonts::NotoSansCjkJpMedium24pt;
  }

  $reorder next 1
}

$rect <20,210,260,250>
$output false
class DEV_TFTBrightness : Components::BaseMainBG
{
  $rect <10,340,190,380>
  inherited method Init()
  {
    BrightnessLevel = DeviceInterface::SystemDevice.BrightnessLevel;

  }

  $rect <10,290,190,330>
  inherited property Bounds = <0,0,480,272>;

  $rect <10,390,190,430>
  inherited method UpdateViewState()
  {
    super( aState );
    trace "aState", aState;
    var Application::Application App = ( Application::Application )GetRoot();
    trace "app: ", App;
    if( null != App )
    {
      if( aState.contains( Core::ViewState[ Dialog ] ) )
      {
        App.StatusBarVisible = false;
      }
      else
      {
        App.StatusBarVisible = true;
      }
    }
  }

  $rect <500,60,700,100>
  inherited method OnShortDownKeyActivated()
  {
    var int32 NextBrightnessLevel = BrightnessLevel - 1;
    if( 0 <= NextBrightnessLevel )
    {
      BrightnessLevel = NextBrightnessLevel;
    }
    else
    {
      ShowLevelText();
    }
  }

  $rect <500,10,700,50>
  inherited method OnShortUpKeyActivated()
  {
    var int32 NextBrightnessLevel = BrightnessLevel + 1;
    if( BrightnessList.size > NextBrightnessLevel )
    {
      BrightnessLevel = NextBrightnessLevel;
    }
    else
    {
      ShowLevelText();
    }
  }

  $rect <500,110,730,150>
  inherited method OnShortHomeKeyActivated()
  {
    Owner.DismissDialog( this, null, null, null, null, null, false );
  }

  $rect <20,20,160,60>
  object Views::Wallpaper ChessboardImage
  {
    preset Bounds = <0,2,480,272>;
    preset Bitmap = Resource::Chessboard;
  }

  $rect <20,20,160,60>
  object Views::Text LevelText
  {
    preset Bounds = <432,2,482,22>;
    preset WrapText = true;
    preset String = "";
    preset Font = Fonts::NotoSansCjkJpMedium24pt;
  }

  $rect <500,210,700,250>
  property int32 BrightnessLevel = -1;

  $rect <500,260,700,300>
  onset BrightnessLevel
  {
    if( pure BrightnessLevel != value )
    {
      pure BrightnessLevel = value;
      LevelText.String = string( BrightnessLevel );
      ShowLevelText();
      if( DeviceInterface::SystemDevice.BrightnessLevel != value )
      {
        DeviceInterface::SystemDevice.BrightnessLevel = value;
      }
      DeviceInterface::SystemDevice.SetTFTDutyCycle( BrightnessList[BrightnessLevel] );
    }
  }

  $rect <500,160,700,200>
  array int32 BrightnessList[ 11 ] =
  (
    Default[0] = 1;
    Default[1] = 3;
    Default[2] = 6;
    Default[3] = 11;
    Default[4] = 18;
    Default[5] = 27;
    Default[6] = 38;
    Default[7] = 50;
    Default[8] = 65;
    Default[9] = 81;
    Default[10] = 100;
  );

  $rect <500,310,700,350>
  object Core::Timer HideLevelTimer
  {
    preset OnTrigger = OnHideLevelSlot;
  }

  $rect <500,360,700,400>
  slot OnHideLevelSlot
  {
    HideLevelTimer.Enabled = false;
    LevelText.Visible = false;
  }

  $rect <500,410,700,450>
  method void ShowLevelText()
  {
    LevelText.Visible = true;
    HideLevelTimer.Enabled = true;
  }
}

$rect <20,260,260,300>
$output false
class DEV_MotoCon : Menu::BaseMenuView
{
  $rect <10,300,210,340>
  inherited property Bounds = <0,0,480,272>;

  $rect <230,300,460,340>
  inherited method OnShortHomeKeyActivated()
  {
    Owner.DismissDialog( this, null, null, null, null, null, false );
  }

  $rect <510,10,710,50>
  inherited method LoadItemClass()
  {
    aItemNo; // suppress warning
    return Menu::ItemBase;
  }

  $rect <510,60,710,100>
  inherited method LoadItemTitle()
  {
    var string Title = "";
    if( aItemNo < ItemTitleArray.size )
    {
      Title = ItemTitleArray[aItemNo];
    }
    return Title;
  }

  $rect <510,110,710,150>
  inherited method OnItemActivate()
  {
    // suppress warning
    aMenuItem;
    aItemNo;

    $if !$prototyper
    native( aItemNo )
    {
      TEST_motocon_tx( aItemNo );
    }
    $endif
  }

  $rect <20,20,160,60>
  inherited object Menu
  {
    preset NoOfItems = ItemTitleArray.size;
    preset ItemHeight = Menu::ItemHeightSmall;
    preset ItemNumPerPage = 4;
  }

  $rect <730,60,930,100>
  array string ItemTitleArray[ 28 ] =
  (
    Default[0] = "Start BLE advertising";
    Default[1] = "Stop BLE advertising";
    Default[2] = "Send library version";
    Default[3] = "Send weather request";
    Default[4] = "Send vehicle info";
    Default[5] = "Send vehicle setting req";
    Default[6] = "Send lang type request";
    Default[7] = "Send CAN related data";
    Default[8] = "Send CAN response";
    Default[9] = "Send injection quality";
    Default[10] = "Send music control play";
    Default[11] = "Send music control pause";
    Default[12] = "Send music control previous";
    Default[13] = "Send music control next";
    Default[14] = "Send incoming call answer";
    Default[15] = "Send incoming call decline";
    Default[16] = "Send headset state request";
    Default[17] = "Send volume control up";
    Default[18] = "Send volume control down";
    Default[19] = "Send mute microphone";
    Default[20] = "Send mute speaker";
    Default[21] = "Send media volume up";
    Default[22] = "Send media volume down";
    Default[23] = "Send vol controllable req";
    Default[24] = "Send create OTA request";
    Default[25] = "Send OTA LinkCard info";
    Default[26] = "Send CCUID response";
    Default[27] = "Send phone signal level req";
  );

  $reorder SlideInDialog 4
  $reorder LoadItemClass 4
  $reorder LoadItemTitle 4
  $reorder OnItemActivate 4
  $reorder LoadItemChecked 4
  $reorder ItemTitleArray 1
}

$rect <20,310,260,350>
$output false
class DEV_VehicleSupportedFunction : Menu::BaseMenuView
{
  $rect <10,300,210,340>
  inherited property Bounds = <0,0,480,272>;

  $rect <230,300,460,340>
  inherited method OnShortHomeKeyActivated()
  {
    Owner.DismissDialog( this, null, null, null, null, null, false );
  }

  $rect <510,10,710,50>
  inherited method LoadItemClass()
  {
    // suppress warning
    if( aItemNo >= 0 )
    {
      // empty body
    }
    return Menu::ItemCheckbox;
  }

  $rect <510,60,710,100>
  inherited method LoadItemTitle()
  {
    var string Title = "";
    if( aItemNo < ItemTitleArray.size )
    {
      Title = ItemTitleArray[aItemNo];
    }
    return Title;
  }

  $rect <510,160,710,200>
  inherited method OnItemActivate()
  {
    // suppress warning
    aMenuItem;
    aItemNo;

    $if !$prototyper
      var bool IsChecked = false;
      var Menu::ItemCheckbox CheckBoxItem = ( Menu::ItemCheckbox )aMenuItem;
      if( null != CheckBoxItem )
      {
        IsChecked = CheckBoxItem.Checked;
      }

      var int32 VehicleFeatureNum = Enum::VehicleSupportedFunction.TOTAL;
      native( aItemNo, IsChecked, VehicleFeatureNum )
      {
        if( 0 == aItemNo )
        {
          for( int i = 0; i < VehicleFeatureNum; i++ )
          {
            VI_set_supported_function( i, IsChecked );
          }
        }
        else
        {
          // offset 1 for the toggle all option
          VI_set_supported_function( aItemNo - 1 , IsChecked );
        }
      }

      if( 0 == aItemNo )
      {
        Menu.InvalidateItems( 1, Menu.NoOfItems - 1 );
      }
    $endif
  }

  $rect <510,110,710,150>
  inherited method LoadItemChecked()
  {
    var bool IsChecked = false;
    $if !$prototyper
      if( 0 == aItemNo )
      {
        var bool IsAllEnabled = true;
        var int32 i;
        for( i = 0; i < (int32)Enum::VehicleSupportedFunction.TOTAL; i++ )
        {
          if( !DeviceInterface::VehicleDevice.IsVehicleFunctionSupported( (Enum::VehicleSupportedFunction)i ) )
          {
            IsAllEnabled = false;
            break;
          }
        }
        IsChecked = IsAllEnabled;
      }
      else
      {
        IsChecked = DeviceInterface::VehicleDevice.IsVehicleFunctionSupported( (Enum::VehicleSupportedFunction)( aItemNo - 1 ) ); // offset 1 for the toggle all option
      }
    $else
      aItemNo; // suppress warning
    $endif
    return IsChecked;
  }

  $rect <20,20,160,60>
  inherited object Menu
  {
    preset NoOfItems = ItemTitleArray.size;
    preset ItemHeight = Menu::ItemHeightSmall;
    preset ItemNumPerPage = 4;
    preset ScrollbarVisible = false;
  }

  // The sequence is the same as the Enum::VehicleSupportedFunction.
  $rect <730,60,930,100>
  array string ItemTitleArray[ 28 ] =
  (
    Default[0] = "Toggle All";
    Default[1] = "Trip 1";
    Default[2] = "Trip 2";
    Default[3] = "F-Trip";
    Default[4] = "Meter Brightness Adj";
    Default[5] = "Clock";
    Default[6] = "TCS";
    Default[7] = "Grip Warmer";
    Default[8] = "Seat Heater";
    Default[9] = "Wind Screen";
    Default[10] = "Oil Trip";
    Default[11] = "V-Belt Trip";
    Default[12] = "Free 1";
    Default[13] = "Free 2";
    Default[14] = "Avg Speed";
    Default[15] = "Current Fuel";
    Default[16] = "Avg Fuel";
    Default[17] = "Fuel Consumption";
    Default[18] = "Air Temperature";
    Default[19] = "Battery Voltage";
    Default[20] = "Coolant";
    Default[21] = "Range Distance";
    Default[22] = "Tire Front";
    Default[23] = "Tire Front Right";
    Default[24] = "Tire Front Left";
    Default[25] = "Tire Rear";
    Default[26] = "Trip Time";
    Default[27] = "Cruise";
  );

  $reorder SlideInDialog 4
  $reorder LoadItemClass 4
  $reorder LoadItemTitle 4
  $reorder OnItemActivate 4
  $reorder LoadItemChecked 4
  $reorder ItemTitleArray 1
}

$rect <310,60,550,100>
$output false
class DEV_FontMenu : Menu::BaseMenuView
{
  $rect <510,10,710,50>
  inherited method LoadItemClass()
  {
    // suppress warning
    if( aItemNo >= 0 )
    {
      // empty body
    }

    return Menu::ItemBase;
  }

  $rect <510,60,710,100>
  inherited method LoadItemTitle()
  {
    var string Title = "";
    if( aItemNo < ItemTitleArray.size )
    {
      Title = ItemTitleArray[aItemNo];
    }
    return Title;
  }

  $rect <510,110,710,150>
  inherited method OnItemActivate()
  {
    aMenuItem; // suppress warning

    var Development::DEV_FontTest Dialog = new Development::DEV_FontTest;

    switch( aItemNo )
    {
      case 0:
      {
        Dialog.TestFont = Fonts::NotoSansCjkJpMedium24pt;
        Dialog.TestString = Strings::DEV_TEST_LETTERS;
      }
      case 1:
      {
        Dialog.TestFont = Fonts::NotoSansCjkJpMedium28pt;
        Dialog.TestString = Strings::DEV_TEST_LETTERS;
      }
      case 2:
      {
        Dialog.TestFont = Fonts::NotoSansCjkJpMedium24pt;
        Dialog.TestString = Strings::DEV_TEST_CHINESE;
      }
      case 3:
      {
        Dialog.TestFont = Fonts::NotoSansCjkJpMedium24pt;
        Dialog.TestString = Strings::DEV_TEST_JAPANESE;
      }
      case 4:
      {
        Dialog.TestFont = Fonts::NotoSansCjkJpMedium28pt;
        Dialog.TestString = Strings::DEV_TEST_CHINESE;
      }
      case 5:
      {
        Dialog.TestFont = Fonts::NotoSansCjkJpMedium28pt;
        Dialog.TestString = Strings::DEV_TEST_JAPANESE;
      }
      default:;
    }

    SlideInDialog( Dialog );
  }

  $rect <20,20,160,60>
  inherited object Menu
  {
    preset NoOfItems = ItemTitleArray.size;
    preset ItemHeight = Menu::ItemHeightSmall;
    preset ItemNumPerPage = 4;
  }

  $rect <730,60,930,100>
  array string ItemTitleArray[ 6 ] =
  (
    Default[0] = "NotoSans Med 24pt Letter";
    Default[1] = "NotoSans Med 28pt Letter";
    Default[2] = "NotoSansCJKJP24 Chinese";
    Default[3] = "NotoSansCJKJP24 Japanese";
    Default[4] = "NotoSansCJKJP28 Chinese";
    Default[5] = "NotoSansCJKJP28 Japanese";
  );

  $rect <0,290,200,330>
  var bool SlideOutEffectEnabled1 = true;

  $reorder SlideInDialog 4
  $reorder LoadItemClass 4
  $reorder LoadItemTitle 4
  $reorder OnItemActivate 4
  $reorder LoadItemChecked 4
  $reorder ItemTitleArray 1
}

$rect <310,110,550,150>
$output false
class DEV_FontTest : Components::BaseMainBG
{
  $rect <210,340,440,380>
  inherited method OnShortDownKeyActivated()
  {
    if( ( Text.ScrollOffset.y.abs + PageScrollOffset ) < ( Text.GetNoOfRows() * RowDistance ) )
    {
      Text.ScrollOffset.y -= PageScrollOffset;
    }
  }

  $rect <210,290,440,330>
  inherited method OnShortUpKeyActivated()
  {
    var int32 OffsetY = Text.ScrollOffset.y + PageScrollOffset;
    if( OffsetY <= 0 )
    {
      Text.ScrollOffset.y = OffsetY;
    }
  }

  $rect <0,290,200,330>
  inherited var SlideOutEffectEnabled = true;

  $rect <20,20,160,60>
  object Views::Text Text
  {
    preset Bounds = <10,50,470,236>;
    preset WrapText = true;
    preset Alignment = Views::TextAlignment[AlignHorzLeft, AlignVertTop];
    preset String = "";
    preset Font = Fonts::NotoSansCjkJpMedium24pt;
  }

  // Page scroll offset of row distance 31px * 6row
  $rect <514,239,714,279>
  var int32 PageScrollOffset = 186;

  $rect <510,20,710,60>
  property Resources::Font TestFont;

  $rect <510,60,710,100>
  onset TestFont
  {
    if( pure TestFont != value )
    {
      pure TestFont = value;
      Text.Font = value;

      if( Fonts::NotoSansCjkJpMedium28pt == value )
      {
        RowDistance = Fonts::ROW_DISTANCE_28PT;
      }

      PageScrollOffset = ( Text.Bounds.size.y / RowDistance ) * RowDistance;
      trace "PageScrollOffset: ", PageScrollOffset;
    }
  }

  $rect <514,129,714,169>
  property string TestString;

  $rect <514,169,714,209>
  onset TestString
  {
    if( pure TestString != value )
    {
      pure TestString = value;
      Text.String = value;
    }
  }

  $rect <511,280,711,320>
  var int32 RowDistance = Fonts::ROW_DISTANCE_24PT;
}

$rect <310,160,550,200>
$output false
class DEV_EEPROMTest : Menu::BaseMenuView
{
  $rect <500,20,700,60>
  inherited method LoadItemClass()
  {
    aItemNo; // suppress warning
    return Menu::ItemBase;
  }

  $rect <500,70,700,110>
  inherited method LoadItemTitle()
  {
    var string Title = "";
    if( aItemNo < ItemTitleArray.size )
    {
      Title = ItemTitleArray[aItemNo];
    }
    return Title;
  }

  $rect <500,120,700,160>
  inherited method OnItemActivate()
  {
    aMenuItem; // suppress warning

    $if !$prototyper
      var Enum::EEPROMTest TestItem = (Enum::EEPROMTest)aItemNo;
      native( TestItem )
      {
        TEST_eeprom( TestItem );
      }
    $else
      aItemNo; // suppress warning
    $endif
  }

  $rect <20,20,160,60>
  inherited object Menu
  {
    preset NoOfItems = ItemTitleArray.size;
    preset ItemHeight = Menu::ItemHeightSmall;
    preset ItemNumPerPage = 4;
    preset ArrowScrollBarVisible = true;
  }

  $rect <730,70,930,110>
  array string ItemTitleArray[ 5 ] =
  (
    Default[0] = "Write last page 0xFF";
    Default[1] = "Read last page";
    Default[2] = "Write test UNIT ID";
    Default[3] = "Read UNIT ID";
    Default[4] = "Clear UNIT ID";
  );
}

$rect <310,210,550,250>
$output false
class DEV_LanguageTest : Menu::BaseMenuView
{
  $rect <500,50,680,90>
  inherited method Init()
  {
    GetCurrentLanguage();
  }

  $rect <770,50,970,90>
  inherited method LoadItemClass()
  {
    aItemNo; // suppress warning
    return Menu::ItemCheckMark;
  }

  $rect <770,100,970,140>
  inherited method LoadItemTitle()
  {
    var string Title = "";
    if( (int32)Enum::Language.TOTAL > aItemNo )
    {
      var Enum::Language LanguageType = (Enum::Language)aItemNo;
      switch( LanguageType )
      {
        case Enum::Language.ENGLISH: Title = "English";
        case Enum::Language.FRENCH: Title = "French";
        case Enum::Language.GERMAN: Title = "German";
        case Enum::Language.ITALIAN: Title = "Italian";
        case Enum::Language.JAPANESE: Title = "Japanese";
        case Enum::Language.SPANISH: Title = "Spanish";
        case Enum::Language.TRADITIONAL_CHINESE: Title = "Traditional Chinese";
        case Enum::Language.SIMPLIFIED_CHINESE: Title = "Simplified Chinese";
        case Enum::Language.GREEK: Title = "Greek";
        case Enum::Language.FINNISH: Title = "Finnish";
        case Enum::Language.HINDI: Title = "Hindi";
        case Enum::Language.INDONESIAN: Title = "Indonesian";
        case Enum::Language.POLISH: Title = "Polish";
        case Enum::Language.PORTUGUESE_BRAZIL: Title = "Portuguese_brazil";
        case Enum::Language.THAI: Title = "Thai";
        case Enum::Language.VIETNAMESE: Title = "Vietnamese";
        case Enum::Language.KOREAN: Title = "KoreankOREAN";
        case Enum::Language.MALAY: Title = "Malay";
        default:;
      }
    }
    return Title;
  }

  $rect <770,200,970,240>
  inherited method OnItemActivate()
  {
    // suppress warning
    aMenuItem;
    aItemNo;

    $if !$prototyper
      native
      {
        ew_system_set_language( LANGUAGE_SRC_PRIORITY_PHONE, aItemNo );
      }
    $else
      DeviceInterface::SystemDevice.ChangeLanguage( aItemNo );
    $endif
  }

  $rect <770,150,970,190>
  inherited method LoadItemChecked()
  {
    var bool IsChecked = false;
    if( (int32)Enum::Language.TOTAL > aItemNo )
    {
      var Enum::Language Language = (Enum::Language)aItemNo;
      if( CurrentLanguage == Language )
      {
        IsChecked = true;
      }
    }
    return IsChecked;
  }

  $rect <20,20,160,60>
  inherited object Menu
  {
    preset NoOfItems = Enum::Language.TOTAL;
    preset ItemHeight = Menu::ItemHeightSmall;
    preset ItemNumPerPage = 4;
    preset ArrowScrollBarVisible = true;
  }

  $rect <500,150,710,190>
  var Enum::Language CurrentLanguage;

  $rect <500,200,760,240>
  object Core::SystemEventHandler ReceivedSystemEventHandler
  {
    preset OnEvent = OnSystemEventReceived;
    preset Event = DeviceInterface::SystemDevice.SystemDataReceivedSystemEvent;
  }

  // This slot method is executed when the associated system event handler 'SystemEventHandler' \
  // receives an event.
  $rect <500,250,760,290>
  slot OnSystemEventReceived
  {
    var DeviceInterface::SystemData SystemData = (DeviceInterface::SystemData)ReceivedSystemEventHandler.Context;
    if( null != SystemData )
    {
      switch( SystemData.RxEvent )
      {
        case Enum::SystemRxEvent.LANGUAGE_CHANGED:
        {
          GetCurrentLanguage();
          Menu.InvalidateItems( 0, Menu.NoOfItems - 1 );
        }
        default:;
      }
    }
  }

  $rect <500,100,700,140>
  method void GetCurrentLanguage()
  {
    CurrentLanguage = (Enum::Language)DeviceInterface::SystemDevice.GetSystemStatus( Enum::SystemStatus.LANGUAGE );
  }
}
