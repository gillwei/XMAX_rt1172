$version 11.00

$rect <20,20,220,60>
$output false
class BaseComponent : Core::Group
{
  $rect <30,30,210,70>
  inherited method Init()
  {
    DeviceInterface::SystemDevice.SetKeyTriggerMode( Core::KeyCode.Up, UpKeyTriggerMode );
    DeviceInterface::SystemDevice.SetKeyTriggerMode( Core::KeyCode.Down, DownKeyTriggerMode );
    DeviceInterface::SystemDevice.SetKeyTriggerMode( Core::KeyCode.Ok, EnterKeyTriggerMode );
    DeviceInterface::SystemDevice.SetKeyTriggerMode( Core::KeyCode.Home, HomeKeyTriggerMode );
    DeviceInterface::SystemDevice.SetMagicKeyEnabled( MagicKeyEnabled );
  }

  $rect <40,210,240,250>
  object Core::KeyPressHandler KeyHandler
  {
    preset OnRelease = OnKeyReleaseSlot;
    preset OnPress = OnKeyPressSlot;
    preset OnHold = OnKeyHoldSlot;
  }

  $rect <40,260,240,300>
  slot OnKeyPressSlot
  {
    $if $prototyper
      trace "key event to: ", classof this;
    $endif

    switch( KeyHandler.Code )
    {
      case Core::KeyCode.Up:
      {
        if( PassUpKey )
        {
          KeyHandler.Continue = true;
        }
        else
        {
          if( ( Enum::KeyTriggerMode.ON == UpKeyTriggerMode ) && !IsDDModeEffected() )
          {
            OnShortUpKeyActivated();
          }
        }
      }
      case Core::KeyCode.Down:
      {
        if( PassDownKey )
        {
          KeyHandler.Continue = true;
        }
        else
        {
          if( ( Enum::KeyTriggerMode.ON == DownKeyTriggerMode ) && !IsDDModeEffected() )
          {
            OnShortDownKeyActivated();
          }
        }
      }
      case Core::KeyCode.Ok:
      {
        if( PassEnterKey )
        {
          KeyHandler.Continue = true;
        }
        else
        {
          if( ( Enum::KeyTriggerMode.ON == EnterKeyTriggerMode ) && !IsDDModeEffected() )
          {
            OnShortEnterKeyActivated();
          }
        }
      }
      case Core::KeyCode.Home:
      {
        if( PassHomeKey )
        {
          KeyHandler.Continue = true;
        }
        else
        {
          if( Enum::KeyTriggerMode.ON == HomeKeyTriggerMode )
          {
            OnShortHomeKeyActivated();
          }
        }
      }
      case Core::KeyCode.F9:
      {
        if( PassMagicKey )
        {
          KeyHandler.Continue = true;
        }
        else
        {
          OnShortMagicKeyActivated();
        }
      }
      default:;
    }
  }

  $rect <40,310,240,350>
  slot OnKeyHoldSlot
  {
    if( !IsDDModeEffected() )
    {
      switch( KeyHandler.Code )
      {
        case Core::KeyCode.Up: OnLongUpKeyActivated();
        case Core::KeyCode.Down: OnLongDownKeyActivated();
        case Core::KeyCode.Ok: OnLongEnterKeyActivated();
        case Core::KeyCode.Home: OnLongHomeKeyActivated();
        default:;
      }
    }
  }

  $rect <40,360,240,400>
  slot OnKeyReleaseSlot
  {
    if( !KeyHandler.Repetition )
    {
      switch( KeyHandler.Code )
      {
        case Core::KeyCode.Up:
        {
          if( ( Enum::KeyTriggerMode.OFF == UpKeyTriggerMode ) && !IsDDModeEffected() && DeviceInterface::SystemDevice.IsKeyStateValid() )
          {
            OnShortUpKeyActivated();
          }
          else
          {
            OnUpKeyReleased();
          }
        }
        case Core::KeyCode.Down:
        {
          if( ( Enum::KeyTriggerMode.OFF == DownKeyTriggerMode ) && !IsDDModeEffected() && DeviceInterface::SystemDevice.IsKeyStateValid() )
          {
            OnShortDownKeyActivated();
          }
          else
          {
            OnDownKeyReleased();
          }
        }
        case Core::KeyCode.Ok:
        {
          if( ( Enum::KeyTriggerMode.OFF == EnterKeyTriggerMode ) && !IsDDModeEffected() && DeviceInterface::SystemDevice.IsKeyStateValid() )
          {
            OnShortEnterKeyActivated();
          }
        }
        case Core::KeyCode.Home:
        {
          if( Enum::KeyTriggerMode.OFF == HomeKeyTriggerMode && DeviceInterface::SystemDevice.IsKeyStateValid() )
          {
            OnShortHomeKeyActivated();
          }
        }
        default:;
      }
    }
    else
    {
      switch( KeyHandler.Code )
      {
        case Core::KeyCode.Up: OnUpKeyReleased();
        case Core::KeyCode.Down: OnDownKeyReleased();
        default:;
      }
    }
  }

  $rect <255,210,485,250>
  method void OnShortDownKeyActivated()
  {
  }

  $rect <255,160,485,200>
  method void OnShortUpKeyActivated()
  {
  }

  $rect <255,260,485,300>
  method void OnShortEnterKeyActivated()
  {
  }

  // Key Handle
  note group Note5
  {
    attr Bounds = <20,110,1380,460>;
  }

  $rect <255,310,485,350>
  method void OnShortHomeKeyActivated()
  {
  }

  $rect <950,160,1150,200>
  property Enum::KeyTriggerMode UpKeyTriggerMode = Enum::KeyTriggerMode.ON;

  $rect <950,210,1150,250>
  property Enum::KeyTriggerMode DownKeyTriggerMode = Enum::KeyTriggerMode.ON;

  $rect <950,260,1150,300>
  property Enum::KeyTriggerMode EnterKeyTriggerMode = Enum::KeyTriggerMode.OFF;

  $rect <950,310,1150,350>
  property Enum::KeyTriggerMode HomeKeyTriggerMode = Enum::KeyTriggerMode.OFF;

  $rect <495,210,725,250>
  method void OnLongDownKeyActivated()
  {
  }

  $rect <495,160,725,200>
  method void OnLongUpKeyActivated()
  {
  }

  $rect <495,260,725,300>
  method void OnLongEnterKeyActivated()
  {
  }

  $rect <495,310,725,350>
  method void OnLongHomeKeyActivated()
  {
    if( Components::ACTIVATE_LONG_PRESS_REPETITION_COUNT == KeyHandler.RepetitionCount )
    {
      var Application::Application App = ( Application::Application )GetRoot();
      if( null != App )
      {
        App.ReturnToHome();
      }
    }
  }

  $rect <1160,160,1360,200>
  property bool PassUpKey = false;

  $rect <1160,210,1360,250>
  property bool PassDownKey = false;

  $rect <1161,260,1361,300>
  property bool PassHomeKey = false;

  $rect <1160,310,1360,350>
  property bool PassEnterKey = false;

  $rect <255,360,485,400>
  method void OnShortMagicKeyActivated()
  {
  }

  $rect <1160,360,1360,400>
  property bool PassMagicKey = false;

  $rect <40,520,240,560>
  property bool DDModeEnabled = false;

  $rect <40,620,240,660>
  method bool IsDDModeEffected()
  {
    return ( DDModeEnabled && DeviceInterface::VehicleDevice.DDModeActivated );
  }

  // Driver Distraction Mode
  note group Note7
  {
    attr Bounds = <20,470,260,680>;
  }

  $rect <40,570,240,610>
  onset DDModeEnabled
  {
    if( pure DDModeEnabled != value )
    {
      pure DDModeEnabled = value;
    }
  }

  // Callback when down key of on trigger mode is released
  $rect <734,210,944,250>
  method void OnDownKeyReleased()
  {
  }

  // Callback when up key of on trigger mode is released
  $rect <734,260,944,300>
  method void OnUpKeyReleased()
  {
  }

  $rect <950,360,1150,400>
  property bool MagicKeyEnabled = false;

  $rect <950,410,1160,450>
  onset MagicKeyEnabled
  {
    if( pure MagicKeyEnabled != value )
    {
      pure MagicKeyEnabled = value;
      DeviceInterface::SystemDevice.SetMagicKeyEnabled( value );
    }
  }
}

$rect <20,70,220,110>
$output false
class BaseMainBG : Components::BaseComponent
{
  $rect <220,280,400,320>
  inherited method Init()
  {
    DDModeStateChangedHandler.Enabled = DDModeEnabled;
    UpdateDDModeMask();
  }

  $rect <0,280,200,320>
  inherited property Bounds = <0,0,480,272>;

  $rect <220,330,450,370>
  inherited method OnShortHomeKeyActivated()
  {
    if( IsDDModeEffected() )
    {
      var Application::Application App = ( Application::Application )GetRoot();
      if( null != App )
      {
        App.ReturnToLauncher();
      }
    }
    else
    {
      DismissThisDialog();
    }
  }

  $rect <540,170,740,210>
  inherited onset DDModeEnabled
  {
    super( value );
    DDModeStateChangedHandler.Enabled = value;
  }

  $rect <20,20,160,60>
  object Views::Image MainBottomBG
  {
    preset Bounds = <0,182,480,272>;
    preset Bitmap = Resource::MainBG;
  }

  $rect <20,20,160,60>
  object Views::Rectangle BlackBG
  {
    preset Bounds = <0,38,480,182>;
    preset Color = #000000FF;
  }

  $rect <540,70,790,110>
  object Core::SystemEventHandler DDModeStateChangedHandler
  {
    preset OnEvent = OnDDModeStateChangedSlot;
    preset Enabled = false;
    preset Event = DeviceInterface::VehicleDevice.DDModeStateChangedSystemEvent;
  }

  // This slot method is executed when the associated system event handler 'SystemEventHandler' \
  // receives an event.
  $rect <540,120,790,160>
  slot OnDDModeStateChangedSlot
  {
    UpdateDDModeMask();
  }

  $rect <20,20,160,60>
  object Components::DDModeMask DDModeMask
  {
    preset StackingPriority = 1;
    preset Bounds = <0,36,480,272>;
    preset Enabled = false;
    preset Visible = false;
  }

  $rect <540,220,740,260>
  method void UpdateDDModeMask()
  {
    if( DDModeEnabled )
    {
      // always dimiss the DD mode mask even when this dialog is not the top dialog.
      if( !DeviceInterface::VehicleDevice.DDModeActivated )
      {
        DDModeMask.Visible = false;
      }
      else
      {
        if( IsCurrentDialog() )
        {
          DDModeMask.Visible = true;
        }
      }
    }
  }

  // Driver Distraction Mode
  note group Note5
  {
    attr Bounds = <510,30,820,280>;
  }

  $rect <220,380,420,420>
  method void DismissThisDialog()
  {
    if( SlideOutEffectEnabled )
    {
      SlideOutDialog();
    }
    else
    {
      if( Owner != null )
      {
        // Show FocusFrame if Owner is of menu type
        var Menu::BaseMenuView MenuDialog = ( Menu::BaseMenuView )Owner;
        if( null != MenuDialog )
        {
          MenuDialog.Menu.FocusFrame.Visible = true;
        }

        Owner.DismissDialog( this, Effect::NoSlideOut, null, null, null, null, false );
      }
    }
  }

  $rect <360,490,560,530>
  method void SlideInDialog( arg Components::BaseMainBG aChildDialog )
  {
    if( IsCurrentDialog() && null != aChildDialog )
    {
      ChildDialog = aChildDialog;

      // hide the highlight focus frame of the child menu
      var Menu::BaseMenuView ChildMenu = ( Menu::BaseMenuView )aChildDialog;
      if( null != ChildMenu )
      {
        ChildMenu.Menu.FocusFrame.Visible = false;
        ChildMenu.Menu.HideArrowScrollBar();
        ChildMenu.Menu.HideScrollbar();
      }

      // hide the highlight focus frame of this current menu
      var Menu::BaseMenuView CurrentMenu = ( Menu::BaseMenuView )this;
      if( null != CurrentMenu )
      {
        CurrentMenu.Menu.FocusFrame.Visible = false;
        CurrentMenu.Menu.HideArrowScrollBar();
        CurrentMenu.Menu.HideScrollbar();
      }

      SlideDirection = Core::Direction.Left;
      HideFocusFrameTimer.Enabled = true;
    }
  }

  $rect <670,490,870,530>
  object Core::Timer HideFocusFrameTimer
  {
    preset OnTrigger = OnHideFocusFrameTimeoutSlot;
    preset Period = 50;
  }

  $rect <670,590,940,630>
  slot OnHideFocusFrameTimeoutSlot
  {
    HideFocusFrameTimer.Enabled = false;

    if( Core::Direction.Left == SlideDirection )
    {
      PresentDialogWithSlideInEffect( ChildDialog );
    }
    else
    {
      DismissMenuWithSlideOutEffect();
    }
  }

  $rect <360,540,580,580>
  var Components::BaseMainBG ChildDialog;

  // SlideIn Menu Operation
  note group Note8
  {
    attr Bounds = <330,450,630,700>;
  }

  $rect <360,640,620,680>
  slot OnDialogSlideInCompletedSlot
  {
    // show hightlight frame of the child menu after slide-in animation
    var Menu::BaseMenuView ChildMenu = ( Menu::BaseMenuView )ChildDialog;
    if( null != ChildMenu )
    {
      if( ChildMenu.Menu.NoOfItems > 0 )
      {
        ChildMenu.Menu.FocusFrame.Visible = true;
      }
      if( ChildMenu.Menu.ArrowScrollBarVisible )
      {
        ChildMenu.Menu.RestoreArrowScrollBar();
      }
    }

    ChildDialog = null;
  }

  // Timer to hide highlight
  note group Note9
  {
    attr Bounds = <650,450,950,700>;
  }

  $rect <360,590,620,630>
  method void PresentDialogWithSlideInEffect( arg Core::Group aView )
  {
    if( null != aView )
    {
      PresentDialog( aView, Effect::SlideInTransition, null, null, null, null, null, OnDialogSlideInCompletedSlot, null, false );
    }
  }

  $rect <0,380,210,420>
  var bool SlideOutEffectEnabled = false;

  $rect <30,640,290,680>
  slot OnDialogSlideOutCompletedSlot
  {
    // Set the focus frame of the owner menu visible after sliding-out animation
    var Menu::BaseMenuView OwnerMenu = ( Menu::BaseMenuView )OwnerDialog;
    if( null != OwnerMenu )
    {
      OwnerMenu.Menu.RestoreFocusFrame();
      OwnerMenu.Menu.RestoreArrowScrollBar();
      OwnerMenu.Menu.RestoreScrollbar();
    }
  }

  $rect <30,540,250,580>
  var Components::BaseMainBG OwnerDialog;

  $rect <30,490,230,530>
  method void SlideOutDialog()
  {
    if( IsCurrentDialog() )
    {
      OwnerDialog = ( Components::BaseMainBG )Owner;

      // hide the highlight focus frame of this current menu
      var Menu::BaseMenuView CurrentMenu = ( Menu::BaseMenuView )this;
      if( null != CurrentMenu )
      {
        CurrentMenu.Menu.FocusFrame.Visible = false;
        CurrentMenu.Menu.HideArrowScrollBar();
      }

      SlideDirection = Core::Direction.Right;
      HideFocusFrameTimer.Enabled = true;
    }
  }

  $rect <30,590,290,630>
  method void DismissMenuWithSlideOutEffect()
  {
    Owner.DismissDialog( this, Effect::SlideOutTransition, null, null, OnDialogSlideOutCompletedSlot, null, false );
  }

  // SlideOut Menu Operation
  note group Note10
  {
    attr Bounds = <10,450,310,700>;
  }

  $rect <670,540,870,580>
  var Core::Direction SlideDirection;
}

$rect <20,120,220,160>
$output false
class DDModeMask : Core::Group
{
  $rect <0,250,200,290>
  inherited property Bounds = <0,0,480,236>;

  $rect <20,20,160,60>
  object Views::Rectangle DDModeBG
  {
    preset StackingPriority = 0;
    preset Bounds = <0,0,480,236>;
    preset Color = #000000CD;
  }

  $rect <20,20,160,60>
  object Views::Image DDModeIcon
  {
    preset StackingPriority = 0;
    preset Bounds = <195,37,285,127>;
    preset Bitmap = Resource::IconDDActive;
  }

  $rect <20,20,160,60>
  object Views::Text WarningText
  {
    preset Bounds = <10,138,470,200>;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset String = Strings::DD_WARNING;
    preset Font = Fonts::NotoSansCjkJpMedium24pt;
  }
}

$rect <250,20,610,60>
$output false
const int32 ACTIVATE_LONG_PRESS_REPETITION_COUNT = 1;

$rect <250,70,450,110>
$output false
class BaseText : Views::Text
{
  $rect <10,60,210,100>
  inherited method ReInit()
  {
    postsignal UpdateFontSlot;
  }

  $rect <13,14,213,54>
  inherited method Init()
  {
    postsignal UpdateFontSlot;
  }

  $rect <10,110,210,150>
  slot UpdateFontSlot
  {
    if( Fonts::NotoSansCjkJpMedium24pt == Font )
    {
      switch( language )
      {
        case TraditionalChinese: Font = Fonts::NotoSansCjkTcMedium24pt;
        default:;
      }
    }
    else if( Fonts::NotoSansCjkJpMedium28pt == Font )
    {
      switch( language )
      {
        case TraditionalChinese: Font = Fonts::NotoSansCjkTcMedium28pt;
        default:;
      }
    }
    else if( Fonts::NotoSansCjkTcMedium24pt == Font )
    {
      switch( language )
      {
        case TraditionalChinese:;
        default: Font = Fonts::NotoSansCjkJpMedium24pt;
      }
    }
    else if( Fonts::NotoSansCjkTcMedium28pt == Font )
    {
      switch( language )
      {
        case TraditionalChinese:;
        default: Font = Fonts::NotoSansCjkJpMedium28pt;
      }
    }
    else
    {
      /* empty */
    }
  }
}
