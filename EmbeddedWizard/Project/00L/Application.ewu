$version 10.00

// This is the root component of the entire GUI application.
$rect <20,20,220,60>
$output false
class Application : Core::Root
{
  $rect <10,340,200,380>
  inherited method Init()
  {
    $if $prototyper
      signal OnStartOpeningSlot;

      // for screenshot
      DeviceInterface::SystemDevice.NotifyUpdateLocalTime();
      StatusBar.BtIcon.Visible = true;
      StatusBar.BatteryIcon.Visible = true;
      StatusBar.HeadsetIcon.Visible = true;
      StatusBar.AppIcon.Visible = true;
      StatusBar.SignalLevelIcon.Visible = true;
    $endif
  }

  $rect <10,290,200,330>
  inherited property Bounds = <0,0,480,272>;

  $rect <40,480,270,520>
  slot OnDisclaimerAcceptedSlot
  {
    var Top::TOP01_Disclaimer DisclaimerDialog = (Top::TOP01_Disclaimer)sender;
    if( null != DisclaimerDialog )
    {
      DisclaimerDialog.TopBar.Color = Color::BLACK;
    }

    IsDisclaimerDismissed = true;
    StatusBar.Visible = true;

    if( Enum::HomeType.NAVI_DEFAULT_VIEW == DeviceInterface::SystemDevice.HomeType ||
        Enum::HomeType.NAVI_NEXT_TURN    == DeviceInterface::SystemDevice.HomeType ||
        Enum::HomeType.NAVI_TURN_BY_TURN == DeviceInterface::SystemDevice.HomeType )
    {
      //TODO: check if navigation app is connected / destination is set before switching to the navigation group
      GetRoot().SwitchToDialog( new Pop::POP01_PleaseWait, null, null, null, null, null, null, null, null, null, false );
    }
    else
    {
      var Core::Group HomeDialog = HomeDialogOfHomeType( DeviceInterface::SystemDevice.HomeType );
      if( null != HomeDialog )
      {
        GetRoot().SwitchToDialog( HomeDialog, null, null, null, null, null, null, null, null, null, false );
      }
    }
  }

  $rect <40,430,270,470>
  method void ShowDisclaimer()
  {
    var Top::TOP01_Disclaimer Disclaimer = new Top::TOP01_Disclaimer;
    Disclaimer.OnAcceptButtonClicked = OnDisclaimerAcceptedSlot;
    GetRoot().SwitchToDialog( Disclaimer, null, null, null, null, null, null, null, null, null, false );
  }

  // For disclaimer
  note group Note
  {
    attr Bounds = <10,390,300,590>;
  }

  // Factory Test
  note group Note2
  {
    attr Bounds = <10,620,300,860>;
  }

  $rect <40,660,270,700>
  object Core::SystemEventHandler FactoryTestEventHandler
  {
    preset OnEvent = OnFactoryTestEventSlot;
    preset Event = DeviceInterface::SystemDevice.FactoryTestSystemEvent;
  }

  // This slot method is executed when the associated system event handler 'SystemEventHandler' \
  // receives an event.
  $rect <40,710,270,750>
  slot OnFactoryTestEventSlot
  {
    var Factory::TestContext TestContext = ( Factory::TestContext )FactoryTestEventHandler.Context;
    if( null != TestContext )
    {
      switch( TestContext.TestItem )
      {
        case Enum::FactoryTest.Display:
        {
          CheckOpeningTimer.Enabled = false;
          var Factory::DisplayManual FactoryTestDialog = (Factory::DisplayManual)FindDialogByClass( Factory::DisplayManual );
          if( null == FactoryTestDialog )
          {
            FactoryTestDialog = new Factory::DisplayManual;
            PresentDialog( FactoryTestDialog, null, null, null, null, null, null, null, null, false );
          }
          FactoryTestDialog.PatternIdx = TestContext.Data;
        }
        case Enum::FactoryTest.BurnInStart:
        {
          CheckOpeningTimer.Enabled = false;
          var Factory::DisplayAutoRun DisplayAutoRunDialog = new Factory::DisplayAutoRun;
          DisplayAutoRunDialog.BurnInEnabled = true;
          PresentDialog( DisplayAutoRunDialog, null, null, null, null, null, null, null, null, false );
        }
        case Enum::FactoryTest.Quit:
        {
          DismissFactoryTestDialog();
        }
        default:;
      }
    }
  }

  // For status bar
  note group Note3
  {
    attr Bounds = <330,570,620,730>;
  }

  $rect <360,620,590,660>
  property bool StatusBarVisible = true;

  $rect <360,670,590,710>
  onset StatusBarVisible
  {
    if ( pure StatusBarVisible != value )
    {
      pure StatusBarVisible = value;
      StatusBar.Visible = value;
      trace "StatusBar Visible: ", value;
    }
  }

  // BT FW Update
  note group Note4
  {
    attr Bounds = <640,570,910,730>;
  }

  $rect <670,620,890,660>
  object Core::PropertyObserver BtFwStatusObserver
  {
    preset OnEvent = OnBtFwStatusUpdteSlot;
    preset Outlet = ^DeviceInterface::BluetoothDevice.BtFwStatus;
  }

  // This slot method is executed when the associated property observer 'PropertyObserver' \
  // is notified.
  $rect <670,670,890,710>
  slot OnBtFwStatusUpdteSlot
  {
    if( Enum::BtFwStatus.UPDATE_START == DeviceInterface::BluetoothDevice.BtFwStatus )
    {
      var Settings::BtFwUpdateDialog BtFwDialog = new Settings::BtFwUpdateDialog;
      BtFwDialog.StackingPriority = 1;
      GetRoot().Add( BtFwDialog, 0 );
      GetRoot().BeginModal( BtFwDialog );
    }
  }

  $rect <40,760,270,800>
  method void DismissFactoryTestDialog()
  {
    var Core::Group FactoryTestDialog = FindDialogByClass( Factory::DisplayAutoRun );
    if( null != FactoryTestDialog )
    {
      DismissDialog( FactoryTestDialog, null, null, null, null, null, false );
    }

    FactoryTestDialog = FindDialogByClass( Factory::DisplayManual );
    if( null != FactoryTestDialog )
    {
      DismissDialog( FactoryTestDialog, null, null, null, null, null, false );
    }
  }

  // Switch to home/launcher
  note group Note1
  {
    attr Bounds = <330,390,1270,550>;
  }

  $rect <360,440,560,480>
  method void SwitchToHome( arg Enum::HomeType aHomeType )
  {
    var class HomeClass = HomeClassOfHomeType( aHomeType );

    if( null != HomeClass )
    {
      // check if Home is already in the dialog stack
      var bool IsInDialogStack = false;
      var int32 DialogIdx = 0;
      for( DialogIdx = 0; DialogIdx < CountDialogs(); DialogIdx++ )
      {
        if( HomeClass == classof GetDialogAtIndex( DialogIdx ) )
        {
          IsInDialogStack = true;
          break;
        }
      }

      if( IsInDialogStack )
      {
        // Dismiss the dialogs on the top of the current home dialog
        for( DialogIdx = 0; DialogIdx < CountDialogs(); DialogIdx++ )
        {
          // The dialog lying top-most (the active dialog in context of this component) has the index 0 (zero).
          if( HomeClass == classof GetDialogAtIndex( 0 ) )
          {
            var Home::BaseHome HomeDialog = ( Home::BaseHome )GetDialogAtIndex( 0 );
            if( null != HomeDialog )
            {
              HomeDialog.ReturnToHome();
            }
            break;
          }
          else
          {
            DismissDialog( GetDialogAtIndex( 0 ), null, null, null, null, null, false );
          }
        }
      }
      else
      {
        var Core::Group HomeDialog = HomeDialogOfHomeType( aHomeType );
        if( null != HomeDialog )
        {
          // dismiss all dialogs
          while( CountDialogs() > 0 )
          {
            DismissDialog( GetDialogAtIndex( 0 ), Effect::NoSlideOut, null, null, null, null, true );
          }
          // show the home dialog
          SwitchToDialog( HomeDialog, null, null, null, null, null, null, null, null, null, true );
          DeviceInterface::SystemDevice.HomeType = aHomeType;
        }
      }
    }
  }

  // Unit test to toggle DD mode every 3 second
  $rect <380,780,580,820>
  object Core::Timer DDModeTestTimer
  {
    preset OnTrigger = OnDDModeTestSlot;
    preset Period = 3000;
    preset Enabled = false;
  }

  $rect <380,830,580,870>
  slot OnDDModeTestSlot
  {
    DeviceInterface::VehicleDevice.DDModeActivated = !DeviceInterface::VehicleDevice.DDModeActivated;
    trace "Test DDMode ", DeviceInterface::VehicleDevice.DDModeActivated;
    DeviceInterface::VehicleDevice.NotifyDDModeStateChanged();
  }

  // Unit Test for DD Mode
  note group Note5
  {
    attr Bounds = <330,740,620,890>;
  }

  // Return from the DD mode forbidden UI to the launcher
  $rect <360,490,560,530>
  method void ReturnToLauncher()
  {
    var int32 NoOfDialogs = CountDialogs();
    var int32 i;
    for( i = 0; i < NoOfDialogs; i++ )
    {
      var Launcher::LNC_Main LauncherMain = ( Launcher::LNC_Main )GetDialogAtIndex( i );
      if( null != LauncherMain )
      {
        LauncherMain.DismissChildDialogs();
        break;
      }
    }
  }

  $rect <570,440,790,480>
  method class HomeClassOfHomeType( arg Enum::HomeType aHomeType )
  {
    var class HomeClass = null;
    switch( aHomeType )
    {
      case Enum::HomeType.TACHO_VISUALIZER: HomeClass = Home::HOM11_TachoVisualizer;
      case Enum::HomeType.ECO_VISUALIZER: HomeClass = Home::HOM12_EcoVisualizer;
      case Enum::HomeType.SPEED_VISUALIZER: HomeClass = Home::HOM13_SpeedVisualizer;
      case Enum::HomeType.NAVI_DEFAULT_VIEW: HomeClass = Navigation::NAV01_DefaultView;
      case Enum::HomeType.NAVI_TURN_BY_TURN: HomeClass = Navigation::NAV03_TBTListView;
      case Enum::HomeType.NAVI_NEXT_TURN: HomeClass = Navigation::NAV05_TBTView;
      case Enum::HomeType.VEHICLE_INFO: HomeClass = Home::HOM03_VehicleInfo;
      default:;
    }
    return HomeClass;
  }

  $rect <570,490,790,530>
  method Core::Group HomeDialogOfHomeType( arg Enum::HomeType aHomeType )
  {
    var Core::Group aHomeDialog = null;
    switch( aHomeType )
    {
      case Enum::HomeType.TACHO_VISUALIZER: aHomeDialog = new Home::HOM11_TachoVisualizer;
      case Enum::HomeType.ECO_VISUALIZER: aHomeDialog = new Home::HOM12_EcoVisualizer;
      case Enum::HomeType.SPEED_VISUALIZER: aHomeDialog = new Home::HOM13_SpeedVisualizer;
      case Enum::HomeType.NAVI_DEFAULT_VIEW: aHomeDialog = new Navigation::NAV01_DefaultView;
      case Enum::HomeType.NAVI_TURN_BY_TURN: aHomeDialog = new Navigation::NAV03_TBTListView;
      case Enum::HomeType.NAVI_NEXT_TURN: aHomeDialog = new Navigation::NAV05_TBTView;
      case Enum::HomeType.VEHICLE_INFO: aHomeDialog = new Home::HOM03_VehicleInfo;
      default:;
    }
    return aHomeDialog;
  }

  $rect <530,140,780,180>
  slot OnOpeningFinishedSlot
  {
    var Core::Group OpeningDialog = (Core::Group)sender;
    if( null != OpeningDialog )
    {
      DismissDialog( OpeningDialog, null, null, null, null, null, false );
    }

    IsFactoryModeDialogDisplayed = false;
    DeviceInterface::BluetoothDevice.GetBluetoothEnable();
    if( Enum::OperationMode.NORMAL == DeviceInterface::SystemDevice.OperationMode )
    {
      ShowDisclaimer();
    }
  }

  $rect <530,40,780,80>
  object Core::SystemEventHandler OpeningSystemEventHandler
  {
    preset OnEvent = OnStartOpeningSlot;
    preset Event = DeviceInterface::SystemDevice.OpeningSystemEvent;
  }

  // This slot method is executed when the associated system event handler 'SystemEventHandler' \
  // receives an event.
  $rect <530,90,780,130>
  slot OnStartOpeningSlot
  {
    CheckOpeningTimer.Enabled = false;

    if( Enum::OperationMode.FACTORY == DeviceInterface::SystemDevice.OperationMode )
    {
      var Open::OPN02_FactoryMode FactoryModeDialog = new Open::OPN02_FactoryMode;
      FactoryModeDialog.OnFactoryModeFinished = OnOpeningFinishedSlot;
      SwitchToDialog( FactoryModeDialog, null, null, null, null, null, null, null, null, null, false );
      IsFactoryModeDialogDisplayed = true;
    }
    else
    {
      var Open::OPN01_BootupAnimation BootupAnimationDialog = new Open::OPN01_BootupAnimation;
      BootupAnimationDialog.OnBootupAnimationFinished = OnOpeningFinishedSlot;
      SwitchToDialog( BootupAnimationDialog, null, null, null, null, null, null, null, null, null, false );
    }
  }

  // Opening Flow
  note group Note6
  {
    attr Bounds = <500,0,800,350>;
  }

  $rect <20,20,160,60>
  object StatusBar::Main StatusBar
  {
    preset Bounds = <0,0,480,38>;
    preset Visible = false;
  }

  // Return from the DD mode forbidden UI to the launcher
  $rect <800,440,1000,480>
  method void ReturnToHome()
  {
    trace "ReturnToHome";
    var int32 NoOfDialogs = CountDialogs();
    var int32 i;
    for( i = 0; i < NoOfDialogs; i++ )
    {
      var Home::BaseHome Dialog = ( Home::BaseHome )GetDialogAtIndex( 0 );
      if( null != Dialog )
      {
        Dialog.ReturnToHome();
      }
      else
      {
        DismissDialog( GetDialogAtIndex( 0 ), null, null, null, null, null, false );
      }
    }
  }

  $rect <530,190,780,230>
  object Core::Timer CheckOpeningTimer
  {
    preset OnTrigger = OnCheckOpeningSlot;
    preset Period = 200;
    preset Begin = 500;
    preset Enabled = true;
  }

  $rect <530,240,780,280>
  slot OnCheckOpeningSlot
  {
    var bool IsTFTBacklightOn = DeviceInterface::SystemDevice.IsTFTBacklightOn();
    if( IsTFTBacklightOn &&
        DeviceInterface::SystemDevice.IsOperationModeReady() &&
        Enum::OperationMode.PRODUCTION_TEST != DeviceInterface::SystemDevice.OperationMode )
    {
      trace "TFT bl ", IsTFTBacklightOn;
      CheckOpeningTimer.Enabled = false;
      signal OnStartOpeningSlot;
    }
  }

  $rect <1020,440,1220,480>
  method void SlideInHome( arg Enum::HomeType aHomeType )
  {
    var Core::Group HomeDialog = HomeDialogOfHomeType( aHomeType );
    trace "SlideInHome ", classof HomeDialog;
    PresentDialog( HomeDialog, Effect::SlideInTransition, null, null, null, null, null, OnSlideInHomeFinishedSlot, null, false );
    DeviceInterface::SystemDevice.HomeType = aHomeType;
  }

  $rect <1020,490,1260,530>
  slot OnSlideInHomeFinishedSlot
  {
    var int32 DialogIdx = 1;
    while( 1 < CountDialogs() )
    {
      DismissDialog( GetDialogAtIndex( DialogIdx ), Effect::NoSlideOut, null, null, null, null, false );
    }
  }

  // Phone
  note group PhoneNote
  {
    attr Bounds = <640,740,980,890>;
  }

  $rect <660,780,960,820>
  object Core::SystemEventHandler PhoneCallStateChangedEventHandler
  {
    preset OnEvent = OnPhoneCallStateChangedSlot;
    preset Event = DeviceInterface::NotificationDevice.PhoneCallStateChangedSystemEvent;
  }

  $rect <660,830,960,870>
  slot OnPhoneCallStateChangedSlot
  {
    var Enum::PhoneCallState PhoneCallState = DeviceInterface::NotificationDevice.GetPhoneCallState();
    switch( PhoneCallState )
    {
      case Enum::PhoneCallState.INCOMING_STARTED:
      {
        if( IsDisclaimerDismissed &&
            Enum::OperationMode.NORMAL == DeviceInterface::SystemDevice.OperationMode &&
            !DeviceInterface::SystemDevice.IsRunningReset )
        {
          var Telephone::TEL01_IncomingCall IncomingCallDialog = new Telephone::TEL01_IncomingCall;
          PresentDialog( IncomingCallDialog, null, null, null, null, null, null, null, null, false );
        }
      }
      default:;
    }
  }

  $rect <40,530,270,570>
  var bool IsDisclaimerDismissed = false;

  // Inspection Mode
  note group Note7
  {
    attr Bounds = <10,880,300,1130>;
  }

  $rect <30,920,280,960>
  object Core::SystemEventHandler InspectionModeEventHandler
  {
    preset OnEvent = OnInspectionModeEventSlot;
    preset Event = DeviceInterface::SystemDevice.InspectionModeSystemEvent;
  }

  // This slot method is executed when the associated system event handler 'SystemEventHandler' \
  // receives an event.
  $rect <30,970,280,1010>
  slot OnInspectionModeEventSlot
  {
    if( IsFactoryModeDialogDisplayed )
    {
      switch( DeviceInterface::SystemDevice.InspectionMode )
      {
        case Enum::InspectionMode.DISPLAY: ProcInspectionDisplay();
        case Enum::InspectionMode.END: StopInspection();
        default:;
      }
    }
    else
    {
      DeviceInterface::SystemDevice.SendInspectionResponse( DeviceInterface::SystemDevice.InspectionMode, DeviceInterface::INSPECTION_NEGATIVE_RESPONSE );
    }
  }

  $rect <30,1020,280,1060>
  method void ProcInspectionDisplay()
  {
    var Inspection::TFT_Main InspectionDialog = (Inspection::TFT_Main)GetDialogAtIndex( 0 );
    if( null == InspectionDialog )
    {
      InspectionDialog = new Inspection::TFT_Main;
      PresentDialog( InspectionDialog, null, null, null, null, null, null, null, null, false );
    }
    InspectionDialog.Pattern = DeviceInterface::SystemDevice.InspectionDisplayPattern;

    DeviceInterface::SystemDevice.SendInspectionResponse( Enum::InspectionMode.DISPLAY, (uint8)InspectionDialog.Pattern );
    DeviceInterface::SystemDevice.OperationMode = Enum::OperationMode.INSPECTION;
  }

  $rect <30,1070,230,1110>
  method void StopInspection()
  {
    var Inspection::TFT_Main InspectionDialog = (Inspection::TFT_Main)GetDialogAtIndex( 0 );
    if( null != InspectionDialog )
    {
      DismissDialog( InspectionDialog, null, null, null, null, null, false );
    }
    DeviceInterface::SystemDevice.SendInspectionResponse( Enum::InspectionMode.END, 0 );
    DeviceInterface::SystemDevice.OperationMode = Enum::OperationMode.FACTORY;
  }

  $rect <530,290,790,330>
  var bool IsFactoryModeDialogDisplayed;

  $rect <800,490,1000,530>
  method void SwitchToMeterHome()
  {
    var Enum::HomeType MeterHomeType;
    switch( DeviceInterface::VehicleDevice.CurrentMeterDisplay )
    {
      case Enum::MeterDisplay.TACHOMETER: MeterHomeType = Enum::HomeType.TACHO_VISUALIZER;
      case Enum::MeterDisplay.ECHO_METER: MeterHomeType = Enum::HomeType.ECO_VISUALIZER;
      case Enum::MeterDisplay.SPEED_METER: MeterHomeType = Enum::HomeType.SPEED_VISUALIZER;
      default: MeterHomeType = Enum::HomeType.TACHO_VISUALIZER;
    }
    SwitchToHome( MeterHomeType );
  }
}
